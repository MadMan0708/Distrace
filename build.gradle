//
// Distrace build file
// This is a multi-module build file.
//
apply from: 'gradle/java.gradle'

// The build script settings to fetch plugins and put them on classpath
buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
        classpath "gradle.plugin.fr.echoes.gradle.plugins:cpp-project-plugin:1.2.9"
    }
}

ext {
    javaProjects = [
            project(':distrace-instrumenter'),
            project(':distrace-agent-attacher')
    ]
    javaProjects.addAll(rootProject.childProjects.values().findAll { it.name.startsWith("distrace-examples") })

    cppProjects = [
            project(':distrace-agent')
    ]

}
//
// For all projects (this and all subprojects) specify common properties and tasks
//
configure(allprojects) { project ->
    apply plugin: 'idea'
}


//
// Common configuration for all subprojects
//
configure(subprojects) { project ->
    // All project inherits the same versioning number
    ext.version = rootProject.version

    repositories {
        mavenCentral()
        maven { url "https://clojars.org/repo" }
    }

    if(project in javaProjects){
        apply from: "$rootDir/gradle/java.gradle"
    }

    if(project in cppProjects){
        // CMake plugin
        apply plugin: "fr.echoes.gradle.cpp"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.11'
}